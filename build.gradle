buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.github.nerdthened.jartighten' version '1.1.0'
}

apply plugin: 'java'

group = 'com.github.NeRdTheNed.deft4j'

sourceCompatibility = 1.8
targetCompatibility = 1.8

final def compiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(8)
}

// Tells Gradle to use the previously defined Java 8 compatible JDK.
java {
    toolchain {
        compiler
    }
}

// Tells Gradle to always compile with the previously defined Java 8 compatible JDK.
tasks.withType(JavaCompile).configureEach {
    javaCompiler = compiler
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    // Tells Gradle to include more debug information in compiled .class files.
    options.debug = true
    options.debugOptions.debugLevel = 'source,lines,vars'
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

shadowJar {
    //minimize()

    archiveClassifier = 'all'
}

jartighten {
    dependsOn(shadowJar)

    inputFile = layout.projectDirectory.file('build/libs/' + project.name + '-all.jar')
    outputFile = layout.projectDirectory.file('build/libs/' + project.name + '-all.jar')

    removeTimestamps = true
    removeComments = true
    removeExtra = true
    removeDirectoryEntries = true
}

build.finalizedBy(jartighten)

dependencies {
    // TODO Remove this dependency
    implementation 'org.apache.commons:commons-compress:1.23.0'

    // picocli
    implementation 'info.picocli:picocli:4.7.4'

    // LL-zip
    implementation 'software.coley:lljzip:2.1.2'
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-jdk14:2.0.7'

    // Compression
    implementation 'com.github.eustas:CafeUndZopfli:5cdf283e67'
    implementation 'com.github.luccappellaro:jzopfli:0.0.4'
    implementation 'org.jruby:jzlib:1.1.5'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.github.NeRdTheNed.deft4j.Main'
        )
    }
}

build.finalizedBy(shadowJar)
